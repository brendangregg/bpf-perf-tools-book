#!/usr/local/bin/bpftrace
/*
 * socksize - Show socket I/O sizes as per-process histograms.
 *
 * See BPF Performance Tools, Chapter 10, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-13: 9780136554820
 * When copying or porting, include this comment.
 *
 * 12-Apr-2019  Brendan Gregg   Created this.
 */

#include <linux/fs.h>
#include <net/sock.h>

kprobe:sock_recvmsg,
kprobe:sock_sendmsg
{
	@socket[tid] = arg0;
}

kretprobe:sock_recvmsg
{
	if (retval < 0x7fffffff) {
		@read_bytes[comm] = hist(retval);
	}
	delete(@socket[tid]);
}

kretprobe:sock_sendmsg
{
	if (retval < 0x7fffffff) {
		@write_bytes[comm] = hist(retval);
	}
	delete(@socket[tid]);
}

END
{
	clear(@socket);
}
