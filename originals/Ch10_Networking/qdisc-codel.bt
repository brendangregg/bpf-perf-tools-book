#!/usr/local/bin/bpftrace
/*
 * qdisc-codel - Show qdisc CoDel queue latency.
 *
 * See BPF Performance Tools, Chapter 10, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-iiiii
 * When copying or porting, include this comment.
 *
 * 21-Apr-2019  Brendan Gregg   Created this.
 */

BEGIN
{
	printf("Tracing qdisc codel latency. Hit Ctrl-C to end.\n");
}

kprobe:codel_qdisc_enqueue
{
	@start[arg0] = nsecs;
}

kretprobe:codel_qdisc_dequeue
/@start[retval]/
{
	@us = hist((nsecs - @start[retval]) / 1000);
	delete(@start[retval]);
}

END
{
	clear(@start);
}
