#!/usr/local/bin/bpftrace
/*
 * nettxlat-dev - Show net device transmission latency by device name.
 *
 * See BPF Performance Tools, Chapter 10, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-iiiii
 * When copying or porting, include this comment.
 *
 * 21-Apr-2019  Brendan Gregg   Created this.
 */

#include <linux/skbuff.h>
#include <linux/netdevice.h>

BEGIN
{
	printf("Tracing net device xmit queue latency. Hit Ctrl-C to end.\n");
}

tracepoint:net:net_dev_start_xmit
{
	@start[args->skbaddr] = nsecs;
}

tracepoint:skb:consume_skb
/@start[args->skbaddr]/
{
	$skb = (struct sk_buff *)args->skbaddr;
	@us[$skb->dev->name] = hist((nsecs - @start[args->skbaddr]) / 1000);
	delete(@start[args->skbaddr]);
}

tracepoint:net:net_dev_queue
{
	// avoid timestamp reuse:
	delete(@start[args->skbaddr]);
}

END
{
	clear(@start);
}
