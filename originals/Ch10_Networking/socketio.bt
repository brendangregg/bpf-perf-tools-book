#!/usr/local/bin/bpftrace
/*
 * socketio - Summarize socket I/O bytes and counts.
 *
 * See BPF Performance Tools, Chapter 10, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-13: 9780136554820
 * When copying or porting, include this comment.
 *
 * 11-Apr-2019  Brendan Gregg   Created this.
 */

#include <net/sock.h>

kprobe:sock_recvmsg
{
	$sock = (struct socket *)arg0;
	$dport = $sock->sk->__sk_common.skc_dport;
	$dport = ($dport >> 8) | (($dport << 8) & 0xff00);
	@io[comm, pid, "read", $sock->sk->__sk_common.skc_prot->name, $dport] =
	    count();
}

kprobe:sock_sendmsg
{
	$sock = (struct socket *)arg0;
	$dport = $sock->sk->__sk_common.skc_dport;
	$dport = ($dport >> 8) | (($dport << 8) & 0xff00);
	@io[comm, pid, "write", $sock->sk->__sk_common.skc_prot->name, $dport] =
	    count();
}
