#!/usr/local/bin/bpftrace
/*
 * dcstat - Directory cache hit statistics.
 *
 * See BPF Performance Tools, Chapter 8, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-13: 9780136554820
 * When copying or porting, include this comment.
 *
 * 26-Mar-2019  Brendan Gregg   Created this.
 */

BEGIN
{
	printf("Tracing dcache lookups... Hit Ctrl-C to end.\n");
	printf("%10s %10s %5s%\n", "REFS", "MISSES", "HIT%");
}

kprobe:lookup_fast { @refs++; }

kretprobe:d_lookup /retval == 0/ { @misses++; }

interval:s:1
{
	$hits = @refs - @misses;
	$percent = @refs > 0 ? 100 * $hits / @refs : 0;
	printf("%10d %10d %4d%%\n", @refs, @misses, $percent);
	clear(@refs);
	clear(@misses);
}

END
{
	clear(@refs);
	clear(@misses);
}
