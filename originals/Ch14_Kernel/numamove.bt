#!/usr/local/bin/bpftrace
/*
 * numamove - Show numa page migration statistics.
 *
 * See BPF Performance Tools, Chapter 14, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-13: 9780136554820
 * When copying or porting, include this comment.
 *
 * 26-Jan-2019  Brendan Gregg   Created this.
 */

kprobe:migrate_misplaced_page { @start[tid] = nsecs; }

kretprobe:migrate_misplaced_page /@start[tid]/
{
	$dur = nsecs - @start[tid];
	@ns += $dur;
	@num++;
	delete(@start[tid]);
}

BEGIN
{
	printf("%-10s %18s %18s\n", "TIME",
	    "NUMA_migrations", "NUMA_migrations_ms");
}

interval:s:1
{
	time("%H:%M:%S");
	printf("   %18d %18d\n", @num, @ns / 1000000);
	delete(@num);
	delete(@ns);
}
