#!/usr/local/bin/bpftrace
/*
 * seeksize - Show requested I/O seek distances.
 *
 * See BPF Performance Tools, Chapter 9, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-13: 9780136554820
 * When copying or porting, include this comment.
 *
 * 18-Oct-2018  Brendan Gregg   Created this.
 */

BEGIN
{
	printf("Tracing block I/O requested seeks... Hit Ctrl-C to end.\n");
}

tracepoint:block:block_rq_issue
{
	if (@last[args->dev]) {
		// calculate requested seek distance
		$last = @last[args->dev];
		$dist = (args->sector - $last) > 0 ?
		    args->sector - $last : $last - args->sector;

		// store details
		@sectors[args->comm] = hist($dist);
	}
	// save last requested position of disk head
	@last[args->dev] = args->sector + args->nr_sector;
}

END
{
	clear(@last);
}
