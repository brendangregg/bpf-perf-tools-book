#!/usr/bin/env bpftrace
/*
 * biolatency-tp.bt - Block I/O latency as a histogram, using tracepoints.
 *
 * See BPF Performance Tools, Chapter 9, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-iiiii
 * When copying or porting, include this comment.
 *
 * 19-Mar-2019  Brendan Gregg   Created this.
 */

BEGIN
{
	printf("Tracing block device I/O... Hit Ctrl-C to end.\n");
}

tracepoint:block:block_rq_issue
{
	@start[args->dev, args->sector] = nsecs;
}

tracepoint:block:block_rq_complete
/@start[args->dev, args->sector]/
{
	@usecs = hist((nsecs - @start[args->dev, args->sector]) / 1000);
	delete(@start[args->dev, args->sector]);
}

END
{
	clear(@start);
}
