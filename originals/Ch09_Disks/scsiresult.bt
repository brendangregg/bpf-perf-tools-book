#!/usr/local/bin/bpftrace
/*
 * scsiresult - Show SCSI command result codes.
 *
 * See BPF Performance Tools, Chapter 9, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-iiiii
 * When copying or porting, include this comment.
 *
 * 21-Mar-2019  Brendan Gregg   Created this.
 */

BEGIN
{
	printf("Tracing scsi command results. Hit Ctrl-C to end.\n");

	// host byte codes, from include/scsi/scsi.h:
	@host[0x00] = "DID_OK";
	@host[0x01] = "DID_NO_CONNECT";
	@host[0x02] = "DID_BUS_BUSY";
	@host[0x03] = "DID_TIME_OUT";
	@host[0x04] = "DID_BAD_TARGET";
	@host[0x05] = "DID_ABORT";
	@host[0x06] = "DID_PARITY";
	@host[0x07] = "DID_ERROR";
	@host[0x08] = "DID_RESET";
	@host[0x09] = "DID_BAD_INTR";
	@host[0x0a] = "DID_PASSTHROUGH";
	@host[0x0b] = "DID_SOFT_ERROR";
	@host[0x0c] = "DID_IMM_RETRY";
	@host[0x0d] = "DID_REQUEUE";
	@host[0x0e] = "DID_TRANSPORT_DISRUPTED";
	@host[0x0f] = "DID_TRANSPORT_FAILFAST";
	@host[0x10] = "DID_TARGET_FAILURE";
	@host[0x11] = "DID_NEXUS_FAILURE";
	@host[0x12] = "DID_ALLOC_FAILURE";
	@host[0x13] = "DID_MEDIUM_ERROR";

	// status byte codes, from include/scsi/scsi_proto.h:
	@status[0x00] = "SAM_STAT_GOOD";
	@status[0x02] = "SAM_STAT_CHECK_CONDITION";
	@status[0x04] = "SAM_STAT_CONDITION_MET";
	@status[0x08] = "SAM_STAT_BUSY";
	@status[0x10] = "SAM_STAT_INTERMEDIATE";
	@status[0x14] = "SAM_STAT_INTERMEDIATE_CONDITION_MET";
	@status[0x18] = "SAM_STAT_RESERVATION_CONFLICT";
	@status[0x22] = "SAM_STAT_COMMAND_TERMINATED";
	@status[0x28] = "SAM_STAT_TASK_SET_FULL";
	@status[0x30] = "SAM_STAT_ACA_ACTIVE";
	@status[0x40] = "SAM_STAT_TASK_ABORTED";
}

tracepoint:scsi:scsi_dispatch_cmd_done
{
	@[@host[(args->result >> 16) & 0xff], @status[args->result & 0xff]] =
	    count();
}

END
{
	clear(@status);
	clear(@host);
}
