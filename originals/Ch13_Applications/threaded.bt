#!/usr/local/bin/bpftrace
/*
 * threaded - Sample on-CPU threads.
 *
 * See BPF Performance Tools, Chapter 13, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-13: 9780136554820
 * When copying or porting, include this comment.
 *
 * 16-Feb-2019  Brendan Gregg   Created this.
 */

BEGIN
{
	if ($1 == 0) {
		printf("USAGE: threaded.bt PID\n");
		exit();
	}
	printf("Sampling PID %d threads at 99 Hertz. Ctrl-C to end.\n", $1);
}

profile:hz:99
/pid == $1/
{
	@[comm, tid] = count();
}

interval:s:1
{
	time();
	print(@);
	clear(@);
}
