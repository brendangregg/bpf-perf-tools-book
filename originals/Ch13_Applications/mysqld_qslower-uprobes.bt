#!/usr/local/bin/bpftrace
/*
 * mysqld_qslower-uprobes - Show MySQL queries slower than a threshold.
 *
 * See BPF Performance Tools, Chapter 13, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-iiiii
 * When copying or porting, include this comment.
 *
 * 15-Feb-2019  Brendan Gregg   Created this.
 */

BEGIN
{
	printf("Tracing mysqld queries slower than %d ms. Ctrl-C to end.\n",
	    $1);
	printf("%-10s %-6s %6s %s\n", "TIME(ms)", "PID", "MS", "QUERY");
}

uprobe:/usr/sbin/mysqld:*dispatch_command*
{
	$COM_QUERY = 3;		// see include/my_command.h
	if (arg2 == $COM_QUERY) {
		@query[tid] = str(*arg1);
		@start[tid] = nsecs;
	}
}

uretprobe:/usr/sbin/mysqld:*dispatch_command*
/@start[tid]/
{
	$dur = (nsecs - @start[tid]) / 1000000;
	if ($dur > $1) {
		printf("%-10u %-6d %6d %s\n", elapsed / 1000000,
		    pid, $dur, @query[tid]);
	}
	delete(@query[tid]);
	delete(@start[tid]);
}
