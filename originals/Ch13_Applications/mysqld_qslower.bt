#!/usr/local/bin/bpftrace
/*
 * mysqld_qslower - Show MySQL queries slower than a threshold.
 *
 * See BPF Performance Tools, Chapter 13, for an explanation of this tool.
 *
 * Copyright (c) 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-13: 9780136554820
 * When copying or porting, include this comment.
 *
 * 15-Feb-2019  Brendan Gregg   Created this.
 */

BEGIN
{
	printf("Tracing mysqld queries slower than %d ms. Ctrl-C to end.\n",
	    $1);
	printf("%-10s %-6s %6s %s\n", "TIME(ms)", "PID", "MS", "QUERY");
}

usdt:/usr/sbin/mysqld:mysql:query__start
{
	@query[tid] = str(arg0);
	@start[tid] = nsecs;
}

usdt:/usr/sbin/mysqld:mysql:query__done
/@start[tid]/
{
	$dur = (nsecs - @start[tid]) / 1000000;
	if ($dur > $1) {
		printf("%-10u %-6d %6d %s\n", elapsed / 1000000,
		    pid, $dur, @query[tid]);
	}
	delete(@query[tid]);
	delete(@start[tid]);
}
