#!/usr/local/bin/bpftrace
/*
 * writesync - Show regular file writes by sync flag.
 *
 * See BPF Performance Tools, Chapter 8, for an explanation of this tool.
 *
 * Copyright 2019 Brendan Gregg.
 * Licensed under the Apache License, Version 2.0 (the "License").
 * This was originally created for the BPF Performance Tools book
 * published by Addison Wesley. ISBN-iiiii
 * When copying or porting, include this comment.
 */

#include <linux/fs.h>
#include <asm-generic/fcntl.h>

BEGIN
{
	printf("Tracing VFS write sync flags... Hit Ctrl-C to end.\n");
}

kprobe:vfs_write,
kprobe:vfs_writev
{
	$file = (struct file *)arg0;
	$name = $file->f_path.dentry->d_name.name;
	if ((($file->f_inode->i_mode >> 12) & 15) == DT_REG) {
		if ($file->f_flags & O_DSYNC) {
			@sync[comm, str($name)] = count();
		} else {
			@regular[comm, str($name)] = count();
		}
	}
}
